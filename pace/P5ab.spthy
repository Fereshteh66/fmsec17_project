theory P5ab
begin

builtins: symmetric-encryption, hashing, diffie-hellman
functions: mac/2, map/2

/*
  We want to model the following protocol

  A -> B {'1', <p, g^x, senc(s)h(kAB)>}
  B -> A {'2', g^y}
  A -> B {'3', <A, g^y>}h(g^xy)
  B -> A {'4', <B, g^x>}h(g^xy)
*/


// Setup a shared key with two parties
rule SharedKeySetup:
  [ Fr(~shk) ]
--[ KeySetup($A, $B), NotEq($A, $B) ]->
  [ !SharedKey($A, $B, ~shk) ]

rule Reveal:
  [ !SharedKey(A, B, kAB) ]
--[ Reveal(A), Reveal(B) ]->
  [ Out(kAB) ]

// Initialize Initiator
rule Init_I:
  [ Fr(~id), !SharedKey(A, B, kAB) ]
--[ Create_I(A, ~id) ]->
  [ St_I_1(A, ~id, kAB, B) ]

// Initialize Responder
rule Init_R:
  [ Fr(~id), !SharedKey(A, B, kAB) ]
--[ Create_R(B, ~id) ]->
  [ St_R_1(B, ~id, kAB, A) ]


// 1. I send
rule I_1_send:
  let g = map(~s, 'p')
      m = <'p', g ^ ~x, senc{~s}h(kAB)>
  in
  [ St_I_1(A, ~id, kAB, B), Fr(~s), Fr(~x) ]
--[ Send(A, <'1', m>) ]->
  [ St_I_2(A, ~id, kAB, ~s, ~x, 'p', B), Out(<'1', m>) ]

// 1. R receive
rule R_1_receive:
  let m = <'p', X, senc{s}h(kAB)>
  in
  [ St_R_1(B, ~id, kAB, A), In(<'1', m>) ]
--[ Recv(B, <'1', m>) ]->
  [ St_R_2(B, ~id, kAB, s, X, 'p', A) ]


// 2. R send
rule R_2_send:
  let g = map(s, 'p')
  in
  [ St_R_2(B, ~id, kAB, s, X, 'p', A), Fr(~y) ]
--[ Send(B, <'2', g ^ ~y >) ]->
  [ St_R_3(B, ~id, kAB, s, X, ~y, 'p', A), Out(<'2', g ^ ~y>) ]


// 2. I receive
rule I_2_receive:
  [ St_I_2(A, ~id, kAB, s, x, 'p', B), In(<'2', Y>) ]
--[ Recv(A, <'2', Y>) ]->
  [ St_I_3(A, ~id, kAB, s, x, Y, 'p', B) ]


// 3. I send
rule I_3_send:
  let g = map(s, 'p')
      m = mac(<A, g ^ x>, h(Y ^ x))
  in
  [ St_I_3(A, ~id, kAB, s, x, Y, 'p', B) ]
--[ Send(A, <'3', m>), Running(A, B, <'R', 'I', <g ^ x, Y, h(Y ^ x)>>) ]->
  [ St_I_4(A, ~id, kAB, s, x, Y, 'p', B), Out(<'3', m>) ]


// 3. R receive
rule R_3_receive:
  let g = map(s, 'p')
      m = mac(<A, X>, h(X ^ y))
  in
  [ St_R_3(B, ~id, kAB, s, X, y, 'p', A), In(<'3', m>) ]
--[ Recv(B, <'3', m>), Running(B, A, <'I', 'R', <X, g ^ y, h(X ^ y)>>), Commit(B, A, <'R', 'I', <X, g ^ y, h(X ^ y)>>), Honest(A), Honest(B), Secret(B, h(X ^ y)) ]->
  [ St_R_4(B, ~id, kAB, s, X, y, 'p', A) ]


// 4. R send
rule R_4_send:
  let g = map(s, 'p')
      m = mac(<B, g ^ y>, h(X ^ y))
  in
  [ St_R_4(B, ~id, kAB, s, X, y, 'p', A) ]
--[ Send(B, <'4', m>) ]->
  [ St_R_5(B, ~id, kAB, s, X, y, 'p', A), Out(<'4', m>) ]


// 4. I receive
rule I_4_receive:
  let g = map(s, 'p')
      m = mac(<B, Y>, h(Y ^ x))
  in
  [ St_I_4(A, ~id, kAB, s, x, Y, 'p', B), In(<'4', m>) ]
--[ Recv(A, <'4', m>), Finish(), Commit(A, B, <'I', 'R', <g ^ x, Y, h(Y ^ x)>>), Secret(A, h(Y ^ x)), Honest(A), Honest(B)  ]->
  [ St_I_5(A, ~id, kAB, s, x, Y, 'p', B) ]



lemma executable:
  exists-trace
    "Ex #i. Finish() @i & not (Ex A #j. Reveal(A)@j)"


lemma secrecy:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


lemma secrecy_PFS:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i & r < i)"
        

lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & j < i
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


lemma noninjectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & j < i
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

/**
  Restrictions
**/
restriction noselfkey:
  " All X Y #i. NotEq(X,Y) @i ==> not (X = Y)"


end