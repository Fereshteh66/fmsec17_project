theory P2a
begin


builtins: symmetric-encryption
functions: mac/2

/*
	We want to formalize the following protocol
	protocol {
		1. A -> B: {'1', x}
		2. B -> A: {'2', y}
		3. A -> B: {'3', y}k(B,A)
		4. B -> A: {'4', x}k(A,B)
	}
*/

// Setup a shared key with two parties
rule SetupKey:
  [ Fr(~shk) ]
--[ KeySetup($A, $B), NotEq($A, $B) ]->
  [ !SharedKey($A, $B, ~shk) ]


rule Reveal:
  [ !SharedKey(A, B, kAB)]
--[ Reveal(A), Reveal(B) ]->
  [ Out(kAB) ]


// Initialize Initiator
rule Init_A:
  [ Fr(~id), !SharedKey(I, R, kAB), !SharedKey(R, I, kBA)]
--[ Create_A(I, ~id) ]->
  [ St_A_1(I, ~id, kAB, kBA, R) ]

// Initialize Responder
rule Init_B:
  [ Fr(~id), !SharedKey(I, R, kAB), !SharedKey(R, I, kBA) ]
--[ Create_B(R, ~id) ]->
  [ St_B_1(R, ~id, kAB, kBA, I) ]

// 1. I send
rule A_1_send:
  [ St_A_1(I, ~id, kAB, kBA, R), Fr(~x) ]
--[ Send(I, <'1', ~x>) ]->
  [ St_A_2(I, ~id, kAB, kBA, ~x, R), Out(<'1', ~x>) ]


// 1. R receive
rule B_1_receive:
  [ St_B_1(R, ~id, kAB, kBA, I), In(<'1', x>) ]
--[ Recv(R, <'1', x>) ]->
  [ St_B_2(R, ~id, kAB, kBA, x, I) ]


// 2. R send
rule B_2_send:
  [ St_B_2(R, ~id, kAB, kBA, x, I), Fr(~y) ]
--[ Send(R, <'2', ~y>), Running(R, I, <'I', 'R', <x, ~y>>) ]->
  [ St_B_3(R, ~id, kAB, kBA, x, ~y, I), Out(<'2', ~y>) ]

// 2. I receive
rule A_2_receive:
  [ St_A_2(I, ~id, kAB, kBA, x, R), In(<'2', y>) ]
--[ Recv(I, <'2', y>), Running(I, R, <'R', 'I', <x, y>>) ]->
  [ St_A_3(I, ~id, kAB, kBA, x, y, R) ]

// 3. I send
rule A_3_send:
  let macA3 = mac(y, kBA)
  in
  [ St_A_3(I, ~id, kAB, kBA, x, y, R) ]
--[ Send(I, <'3', macA3>) ]->
  [ St_A_4(I, ~id, kAB, kBA, x, y, R), Out(<'3', macA3>) ]


// 3. R receive
rule B_3_receive:
  let macB3 = mac(y, kBA)
  in
  [ St_B_3(R, ~id, kAB, kBA, x, y, I), In(<'3', macB3>) ]
--[ Recv(R, <'3', macB3>), Commit(R, I, <'R', 'I', <x, y>>), Honest(I), Honest(R) ]->
  [ St_B_4(R, ~id, kAB, kBA, x, y, I) ]


// 4. R send
rule B_4_send:
  let macB4 = mac(x, kAB)
  in
  [ St_B_4(R, ~id, kAB, kBA, x, y, I) ]
--[ Send(R, <'4', macB4>) ]->
  [ St_B_5(R, ~id, kAB, kBA, x, y, I), Out(<'4', macB4>) ]


// 4. I receive
rule A_4_receive:
  let macA4 = mac(x, kAB)
  in
  [ St_A_4(I, ~id, kAB, kBA, x, y, R), In(<'4', macA4>) ]
--[ Recv(I, <'4', macA4>), Commit(I, R, <'I', 'R', <x, y>>), Honest(I), Honest(R), Finish() ]->
  [ St_A_5(I, ~id, kAB, kBA, x, y, R) ]


/**
	Lemmas
**/

lemma executable:
  exists-trace
    "Ex #i. Finish() @i & not (Ex A #j. Reveal(A)@j)"


lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & j < i
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


lemma noninjectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & j < i
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


/**
	Restrictions
**/
restriction noselfkey:
  " All X Y #i. NotEq(X,Y) @i ==> not (X = Y)"


/* I'm not sure if we can do that, because of a too tight restriction. (refering to discussion of the exercise session) */
restriction noselfcommunication:
  "(All X Y id1 id2 #i #j. Create_A(X, id1)@i & Create_A(Y, id2)@j ==> X = Y & id1 = id2 & #i = #j)
   & (All X Y id1 id2 #i #j. Create_B(X, id1)@i & Create_B(Y, id2)@j ==> X = Y & id1 = id2 & #i = #j)"

restriction onlyonekeysetup:
  " All X Y #i #j. KeySetup(X, Y)@i & KeySetup(X, Y)@j ==> #i = #j"


end