theory P1
begin

builtins: symmetric-encryption
functions: mac/2

/*
	We want to formalize the following protocol
	protocol {
		1. A -> B: {'1', x}
		2. B -> A: {'2', x}k(A,B)
	}
*/


// Setup a shared key with two parties
rule SetupKey:
  [ Fr(~kAB) ]
--[ NotEq($A, $B) ]->
  [ !SharedKey($A, $B, ~kAB) ]


rule Reveal:
  [ !SharedKey(A, B, kAB)]
--[ Reveal(A), Reveal(B) ]->
  [ Out(kAB) ]


// Initialize A
rule Init_A:
  [ Fr(~id) , !SharedKey(I, R, kAB) ]
--[ Create_A(I, ~id) ]->
  [ St_A_1(I, ~id, kAB, R) ]

// Initialize B
rule Init_B:
  [ Fr(~id), !SharedKey(I, R, kAB) ]
--[ Create_B(R, ~id) ]->
  [ St_B_1(R, ~id, kAB, I) ]

// 1. A send
rule A_1_send:
  [ St_A_1(I, ~id, kAB, R), Fr(~x) ]
--[ Send(I, <'1', ~x>) ]->
  [ St_A_2(I, ~id, kAB, ~x, R), Out(<'1', ~x>) ]

// 1. B receive
rule B_1_receive:
  [ St_B_1(R, ~id, kAB, I), In(<'1', x>) ]
--[ Recv(R, <'1', x>), Running(R, I, <'I', 'R', x>) ]->
  [ St_B_2(R, ~id, kAB, x, I) ]


// 2. B send
rule B_2_send:
  let macB2 = mac(x, kAB)
  in
  [ St_B_2(R, ~id, kAB, x, I) ]
--[ Send(R, <'2', macB2>) ]->
  [ St_B_3(R, ~id, kAB, x, I), Out(<'2', macB2>) ]

// 2. A receive
rule A_2_receive:
  let macA2 = mac(x, kAB)
  in
  [ St_A_2(I, ~id, kAB, x, R), In(<'2', macA2>) ]
--[ Recv(I, <'2', macA2>), Commit(I, R, <'I', 'R', x>), Honest(I), Finish() ]->
  [ St_A_3(I, ~id, kAB, x, R) ]


/**
	Lemmas
**/

lemma executable:
  exists-trace
    "Ex #i. Finish() @i & not (Ex A #j. Reveal(A)@j)"

lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & j < i
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


/**
	Restrictions
**/
restriction noselfcommunication:
  " All X Y #i. NotEq(X,Y) @i ==> not (X = Y)"

end