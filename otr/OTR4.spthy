theory OTR4
begin



/*
  We want to formalize the following protocol
	
    1. A -> B: (g^x)
    2. B -> A: (g^y)
    1. A -> B: A, {g^x, g^y}sk(A), MAC{ < '0', A >, K}, pk(A)
    2. B -> A: B, {g^y, g^x}sk(B), MAC{ < '1', A >, K}, pk(B)
*/

builtins: 
  diffie-hellman, signing

functions: 
  mac/2

// Public key infrastructure

rule Register:
 [ Fr(~sk) ]  -->  [ !Ltk($A, ~sk), !Pk($A, pk(~sk)), Out(pk(~sk)) ]

rule Reveal:
 [ !Ltk(A, ltk) ]  --[ Reveal(A) ]->   [ Out(ltk) ]


// Initialize A
rule Init_A:
 [ Fr(~id) , !Ltk($A, skA)  , !Pk($B, pkB) ]
 --[ Create($A, ~id, 'I') ]->
 [ St_A_1($A, ~id, $B, skA, pkB) ]


// Initialize B
rule Init_B:
 [ Fr(~id) , !Ltk($B, skB) , !Pk($A, pkA) ]
 --[ Create($B, ~id, 'R') ]->
 [ St_B_1($B, ~id, $A, skB, pkA) ]


// 1. A send
rule A_1_Send:
 let M = <'1', 'g' ^ ~x>
 in

 [ St_A_1($A, id, $B, skA, pk(skB)) , Fr(~x) ] 
 --[ Send( $A, M )]->
 [ St_A_2($A, id, $B, skA, pk(skB), ~x) , Out( M ) ]


 // 1. B receive
 rule B_1_Recv:
let M = <'1', X>
 in
 [ St_B_1($B, id, $A, skB, pk(skA)), In( M ) ]
 --[ Recv( $B, M ) ]->
 [ St_B_2($B, id, $A, skB, pk(skA), X) ]



 // 2. B send
 rule B_2_Send:
 let M = <'2','g' ^ ~y>
 in

 [ St_B_2($B, id, $A, skB, pk(skA), X) , Fr(~y) ] 
 --[ Send( $B, M )]->
 [ St_B_3($B, id, $A, skB, pk(skA), X, ~y ) , Out( M ) ]


// 2. A receive
rule A_2_Recv:
let M = <'2',Y>
 in

 [ St_A_2($A, id, $B, skA, pk(skB), x), In( M ) ]
 --[ Recv( $A, M ) ]->
 [ St_A_3($A, id, $B, skA, pk(skB), x, Y) ]

// 3. A send
rule A_3_Send:

 let M = <'3', $A,  sign{Y, 'g' ^ x}skA, mac(<'0', $A>, Y ^ x), pk(skA)>
 in

 [ St_A_3($A, id, $B, skA, pk(skB), x, Y) ] 
 --[ Send( $A, M )]->
 [ St_A_4($A, id, $B, skA, pk(skB), x, Y) , Out( M ) ]


// 3. B receive
rule B_3_Recv:

 let M = <'3', $A , sign{'g' ^ y , X}skA, mac(<'0', $A>, X ^ y), pkA>
 in

 [ St_B_3($B, id, $A, skB, pk(skA), X, y), In( M ) ]
 --[ Recv( $B, M ), Claim_secret($B, X ^ y), Honest($A), Honest($B) ]->
 [ St_B_4($B, id, $A, skB, pk(skA), X, y) ]


// 4. B send
rule B_4_Send:

 let M = <'4', $B, sign{'g' ^ y, X }skB, mac(<'1', $B>, X ^ y), pk(skB)>
 in
 [ St_B_4($B, id, $A, skB, pk(skA), X, y) ]
 --[ Send($B, M )]->
 [ St_B_5($B, id, $A, skB, pk(skA), X, y) , Out( M ) ]


// 4. A receive
rule A_4_Recv:

 let M = <'4', $B, sign{Y, 'g' ^ x }skB, mac(<'1', $B>, Y ^ x), pkB>
 in
 [  St_A_4($A, id, $B, skA, pk(skB), x, Y) , In( M ) ]
 --[ Recv( $A, M ), Claim_secret($A, Y ^ x) , Honest($A), Honest($B) ]->
 [ St_A_5($A, id, $B, skA, pk(skB), x, Y)]





// Security properties

lemma Key_Secrecy:
  "All A Kab #i. 
     Claim_secret(A, Kab)@i 
     ==>
     ( not (Ex #k. K(Kab)@k)     
     | (Ex B #r. Reveal(B)@r & Honest(B)@i)
     )
  "

lemma Key_Forward_Secrecy: 
  "All A Kab #i. 
     Claim_secret(A, Kab)@i 
     ==>
     ( not (Ex #k. K(Kab)@k)     
     | (Ex B #r. Reveal(B)@r & Honest(B)@i & r < i)
     )
  "

lemma Executability:
  exists-trace
    "Ex A B Kab #i #j. 
       Claim_secret(A, Kab)@i & Claim_secret(B, Kab)@j & not (A = B) &
       not (Ex B #k. Reveal(B)@k)"


end
